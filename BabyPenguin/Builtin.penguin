namespace __builtin {
    extern fun exit(val code : i32);
    extern fun print(val text : string);
    extern fun println(val text : string);
    fun hello_world() {
        println("hello world!");
    }

    class Box<T> {
        var value: T;
        fun new(var this: Box<T>, var value: T) {
            this.value = value;
        }
    }

    enum Option<T> {
        some: T;
        none;
    
        fun is_some(val this: Option<T>) -> bool {
            return this is Option<T>.some;
        }

        fun is_none(val this: Option<T>) -> bool {
            return this is Option<T>.none;
        }
    
        fun value_or(val this: Option<T>, val default_val: T) -> T {
            if (this is Option<T>.some) {
                return this.some;
            } else {
                return default_val;
            }
        }

        impl __builtin.ICopy<Option<T>> where T: ICopy<T>;
    }

    interface ICopy<T> {
        extern fun copy(val this: T) -> T;
    }
        
    impl __builtin.ICopy<i64> for i64;
    impl __builtin.ICopy<u64> for u64;
    impl __builtin.ICopy<i32> for i32;
    impl __builtin.ICopy<u32> for u32;
    impl __builtin.ICopy<i16> for i16;
    impl __builtin.ICopy<u16> for u16;
    impl __builtin.ICopy<i8> for i8;
    impl __builtin.ICopy<u8> for u8;
    impl __builtin.ICopy<bool> for bool;
    impl __builtin.ICopy<char> for char;
    impl __builtin.ICopy<string> for string;
    impl __builtin.ICopy<float> for float;
    impl __builtin.ICopy<double> for double;

    enum Result<T, E> {
        ok: T;
        error: E;
    
        fun is_ok(val this: Result<T, E>) -> bool {
            return this is Result<T, E>.ok;
        }

        fun is_error(val this: Result<T, E>) -> bool {
            return this is Result<T, E>.error;
        }
    
        fun value_or(val this: Result<T, E>, val default_val: T) -> T {
            if (this is Result<T, E>.ok) {
                return this.ok;
            } else {
                return default_val;
            }
        }

        impl __builtin.ICopy<Result<T, E>> where T: ICopy<T>, E: ICopy<E>;
    }

    interface IIterator<T> {
        fun next(var this: IIterator<T>) -> Option<T>;
    }

    interface IIterable<T> {
        fun iter(var this: IIterable<T>) -> IIterator<T>;
    }

    class RangeIterator {
        val start: i64;
        val end: i64;
        var current: i64;

        fun new(var this: RangeIterator, val start: i64, val end: i64) {
            this.start = start;
            this.end = end;
            this.current = start;
        }

        impl IIterator<i64> {
            fun next(var this: IIterator<i64>) -> Option<i64> {
                var self : RangeIterator = this as RangeIterator;
                if (self.current < self.end) {
                    val res : Option<i64> =new Option<i64>.some(self.current);
                    self.current += 1;
                    return res;
                } else {
                    return new Option<i64>.none();
                }
            }
        }
    }

    fun range(val start: i64, val end: i64) -> IIterator<i64> {
        return new RangeIterator(start, end) as IIterator<i64>;
    }

    class AtomicI64 {
        var value: i64;
        fun new(var this: AtomicI64, val value: i64) {
            this.value = value;
        }
        fun load(val this: AtomicI64) -> i64 {
            return this.value;
        }
        fun store(var this: AtomicI64, val value: i64) {
            this.value = value;
        }
        extern fun swap(var this: AtomicI64, val value: i64) -> i64;
        extern fun compare_exchange(var this: AtomicI64, var current_val: i64, val new_val: i64) -> i64;
        extern fun fetch_add(var this: AtomicI64, val value: i64) -> i64;
    }

    class List<T> {
        var __impl: u64;
        extern fun new(var this: List<T>);
        extern fun at(val this: List<T>, val index: u64) -> Option<T>;
        extern fun remove(val this: List<T>, val index: u64);
        extern fun push(var this: List<T>, val value: T);
        extern fun pop(var this: List<T>) -> Option<T>;
        extern fun size(val this: List<T>) -> u64;

        impl IIterable<T> {
            fun iter(var this: IIterable<T>) -> IIterator<T> {
                return new ListIterator<T>(this as List<T>) as IIterator<T>;
            }
        }
    }

    class ListIterator<T> {
        var list : List<T>;
        var current: u64;

        fun new(var this: ListIterator<T>, var list: List<T>) {
            this.list = list;
            this.current = 0;
        }

        impl IIterator<T> {
            fun next(var this: IIterator<T>) -> Option<T> {
                var self : ListIterator<T> = this as ListIterator<T>;
                var res : Option<T> = self.list.at(self.current);
                self.current += 1;
                return res;
            }
        }
    }

    class Queue<T> {
        var __impl: u64;
        extern fun new(var this: Queue<T>);
        extern fun enqueue(var this: Queue<T>, val value: T);
        extern fun dequeue(var this: Queue<T>) -> Option<T>;
        extern fun peek(var this: Queue<T>) -> Option<T>;
        extern fun size(val this: Queue<T>) -> u64;
    }

    enum RoutineState {
        pending;
        running;
        ready;
        finished;
        ready_finished;

        fun from_i64(val v: i64) -> RoutineState {
            if (v == 0) {
                return new RoutineState.pending();
            } else if (v == 1) {
                return new RoutineState.running();
            } else if (v == 2) {
                return new RoutineState.ready();
            } else if (v == 3) {
                return new RoutineState.finished();
            } else {
                return new RoutineState.ready_finished();
            }
        }

        fun from_atomic(val a: AtomicI64) -> RoutineState {
            val v : i64 = a.load();
            return RoutineState.from_i64(v);
        }

        fun to_i64(val this: RoutineState) -> i64 {
            if (this is RoutineState.pending) {
                return 0;
            } else if (this is RoutineState.running) {
                return 1;
            } else if (this is RoutineState.ready) {
                return 2;
            } else if (this is RoutineState.finished) {
                return 3;
            } else {
                return 4;
            }
        }

        fun switch(var v: AtomicI64, val from: RoutineState, val to: RoutineState) -> bool {
            val from_i64 : i64 = from.to_i64();
            val to_i64 : i64 = to.to_i64();
            val res : i64 = v.compare_exchange(from_i64, to_i64);
            return res == from_i64;
        }
    }

    var __event_id : AtomicI64 = new AtomicI64(0);
    interface IObjectID {
        var object_id : i64 = __event_id.fetch_add(1);
        fun equals(val this: IObjectID, val other: IObjectID) -> bool {
            return this.object_id == other.object_id;
        }
    }

    enum FutureState<T> {
        not_ready;
        ready: T;
        finished;
        ready_finished: T;
    }

    interface IFutureBase {
        fun routine_state(val this: IFutureBase) -> RoutineState;
        fun switch_state(var this: IFutureBase, val from: RoutineState, val to: RoutineState) -> bool;
        fun start(var this: IFutureBase) -> bool;
    }

    interface IFuture<T> {
        impl IFutureBase;
        fun poll(val this: IFuture<T>) -> FutureState<T>;
        
        !async fun do_wait(var this: IFuture<T>) -> T {
            var result: T;
            while(true) {
                wait;
                var state: FutureState<T> = this.poll();
                if (state is FutureState<T>.ready_finished) {
                    result = state.ready_finished;
                    break;
                }
                else if (state is FutureState<T>.ready) {
                    result = state.ready;
                    this.switch_state(new RoutineState.ready(), new RoutineState.pending());
                    _main_scheduler.pending_jobs.enqueue(this);
                } else if (state is FutureState<T>.finished) {
                    break;
                }
            }
            return result;
        }

        !async fun do_wait_any(var this: IFuture<T>) -> Option<T> {
            while(true) {
                wait;
                var state: FutureState<T> = this.poll();
                if (state is FutureState<T>.ready_finished) {
                    this.switch_state(new RoutineState.ready_finished(), new RoutineState.finished());
                    return new Option<T>.some(state.ready_finished);
                }
                else if (state is FutureState<T>.ready) {
                    this.switch_state(new RoutineState.ready(), new RoutineState.pending());
                    _main_scheduler.pending_jobs.enqueue(this);
                    return new Option<T>.some(state.ready);
                } else if (state is FutureState<T>.finished) {
                    break;
                }
            }
            return new Option<T>.none();
        }
    }

    class Scheduler {
        var pending_jobs : Queue<IFutureBase> = new Queue<IFutureBase>();

        fun entry(var this: Scheduler) {
            while (true) {
                var job_opt : Option<IFutureBase> = this.pending_jobs.dequeue();
                if (job_opt.is_some()) {
                    var job : IFutureBase = job_opt.some;
                    val result: bool = job.start();
                    val state : RoutineState = job.routine_state();
                    if (!result) {
                        // job is in invalid state, skip it for now
                        println("Scheduler: job in '" + (state as string) + "' state, failed to run");
                    }
                    else {
                        if (state is RoutineState.pending) {
                            this.pending_jobs.enqueue(job);
                        }
                    }
                } else {
                    break;
                }
            }
        }

        fun current() -> Scheduler {
            return _main_scheduler;
        }
    }

    var _main_scheduler : Scheduler = new Scheduler();

    interface IGenerator<T> {
        impl IIterator<T>;
    }

    class RoutineContext<T> {
        var frame: i64;
        var result: Option<T> = new Option<T>.none();
        var target: async_fun<T>;

        fun new(var this: Self, val target: async_fun<T>) {
            this.target = target;
        }
        extern fun call(var this: Self) -> i64;
    }

    class _DefaultRoutine<T> {
        var state: AtomicI64 = new AtomicI64(0);
        var context: RoutineContext<T>;

        fun new(var this: Self, val target: async_fun<T>, val scheduleNow: bool) {
            this.context = new RoutineContext<T>(target);
            if (scheduleNow) {
                _main_scheduler.pending_jobs.enqueue(this as IFutureBase);
            }
        }

        impl IFutureBase {
            fun routine_state(val this: IFutureBase) -> RoutineState {
                var self: Self = this as Self;
                return RoutineState.from_atomic(self.state);
            }

            fun switch_state(var this: IFutureBase, val from: RoutineState, val to: RoutineState) -> bool {
                var self: Self = this as Self;
                return RoutineState.switch(self.state, from, to);
            }

            fun start(val this: IFutureBase) -> bool {
                var self: Self = this as Self;
                val res : bool = RoutineState.switch(self.state, new RoutineState.pending(), new RoutineState.running());
                if (!res) 
                    return false;
                __signal 0;
                val return_state : i64 = self.context.call();

                // Blocked = 0, YieldNotFinished = 2, Finished = 3, YieldFinished = 4
                RoutineState.switch(self.state, new RoutineState.running(), RoutineState.from_i64(return_state));

                return true;
            }
        }

        impl IFuture<T> {
            fun poll(val this: IFuture<T>) -> FutureState<T> {
                var self: Self = this as Self;
                val state_val: i64 = self.state.load();
                
                if (state_val == 2) {
                    return new FutureState<T>.ready(self.context.result.some);
                } else if (state_val == 3) {
                    return new FutureState<T>.finished();
                } else if (state_val == 4) {
                    return new FutureState<T>.ready_finished(self.context.result.some);
                } else {
                    return new FutureState<T>.not_ready();
                }
            }
        }
    
        impl IGenerator<T>;
        impl IIterator<T> {
            fun next(var this: IIterator<T>) -> Option<T> {
                var self: IFuture<T> = this as IFuture<T>;
                return self.do_wait_any();
            }
        }
    }

    class Event<T> {
        var receivers : List<IEventReceiver<T>> = new List<IEventReceiver<T>>();

        impl IObjectID;

        fun notify(var this: Self, val ev: T) {
            for (var receiver : IEventReceiver<T> in this.receivers.iter()) {
                 receiver.notify(ev);
            }
        }

        fun add_receiver(var this: Self, var receiver: IEventReceiver<T>) {
            for (var r : IEventReceiver<T> in this.receivers.iter()) {
                if (r.equals(receiver))
                    return;
            }
            this.receivers.push(receiver);
        }

        fun remove_receiver(var this: Self, var receiver: IEventReceiver<T>) {
            for (var i : u64 in range(0, this.receivers.size())) {
                var r : Option<IEventReceiver<T>> = this.receivers.at(i);
                if (r.is_some()) {
                    if (r.some.equals(receiver)) {
                        this.receivers.remove(i);
                        return;
                    }
                }
            }
        }
    }

    interface IEventReceiver<T> {
        fun notify(var this: IEventReceiver<T>, val ev: T);
        fun connect(var this: IEventReceiver<T>, var ev: Event<T>);

        impl IFuture<T>;
        impl IObjectID;
    }

    class _QueuedEventReceiver<T> {
        var event_queue : Queue<T> = new Queue<T>();
        var ev : Event<T>;

        fun new(var this: Self, var ev: Event<T>) {
            ev.add_receiver(this);
            this.ev = ev;
        }

        impl IEventReceiver<T> { 
            fun notify(var this: IEventReceiver<T>, val ev: T) {
                var self: Self = this as Self;
                self.event_queue.enqueue(ev);
            }

            fun connect(var this: IEventReceiver<T>, var ev: Event<T>) {
                var self: Self = this as Self;
                self.ev.remove_receiver(self);
                ev.add_receiver(self);
                self.ev = ev;
            }
        }

        impl IFuture<T> {
            fun poll(val this: IFuture<T>) -> FutureState<T> {
                var self: Self = this as Self;
                
                val v : Option<T> = self.event_queue.dequeue();
                if (v.is_some()) {
                     return new FutureState<T>.ready(v.some);
                }
                else {
                    return new FutureState<T>.not_ready();
                }
            }
        }
    }

    class _OnetimeEventReceiver<T> {
        var value : Option<T> = new Option<T>.none();
        var ev : Event<T>;

        fun new(var this: Self, var ev: Event<T>) {
            ev.add_receiver(this);
            this.ev = ev;
        }

        impl IEventReceiver<T> { 
            fun notify(var this: IEventReceiver<T>, val v: T) {
                var self: Self = this as Self;
                self.value = new Option<T>.some(v);
            }

            fun connect(var this: IEventReceiver<T>, var ev: Event<T>) {
                var self: Self = this as Self;
                self.ev.remove_receiver(self);
                ev.add_receiver(self);
                self.ev = ev;
            }
        }

        impl IFuture<T> {
            fun poll(val this: IFuture<T>) -> FutureState<T> {
                var self: Self = this as Self;
                
                if (self.value.is_some()) {
                     return new FutureState<T>.ready_finished(self.value.some);
                }
                else {
                    return new FutureState<T>.not_ready();
                }
            }
        }
    }

}